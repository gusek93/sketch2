plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
    mapstructVersion = '1.5.1.Final'
    querydslVersion = '5.0.0'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.graphql:spring-graphql-test'

    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

    // mapstruct
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    implementation "org.mapstruct:mapstruct:$mapstructVersion"

    // querydsl
    implementation "com.querydsl:querydsl-core:$querydslVersion"
    implementation "com.querydsl:querydsl-jpa:$querydslVersion"
    implementation "com.querydsl:querydsl-sql:$querydslVersion"
    annotationProcessor "com.querydsl:querydsl-apt:$querydslVersion:jpa"

    // datadog log encoder
    implementation 'net.logstash.logback:logstash-logback-encoder:7.2'

    // lombok
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    // database drivers
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'com.h2database:h2'

    // mybatis
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.2'
}

test {
    useJUnitPlatform {
        excludeTags 'IntegrationTest'
    }
}

task integrationTest(type: Test) {
    group 'verification'
    useJUnitPlatform {
        includeTags 'IntegrationTest'
    }
}
